plugins {
    id 'fabric-loom' version "${loom_version}"
    id 'maven-publish'
    id 'io.github.juuxel.loom-quiltflower' version '1.7.3'
    id 'io.freefair.lombok' version '8.6'
    id 'jacoco'
}

version = project.mod_version
group = project.maven_group

loom {
    accessWidenerPath = file("src/main/resources/offlineplayersreworked.accesswidener")
    runtimeOnlyLog4j = true
}

base {
    archivesName = project.archives_base_name
}

repositories {
    // Add repositories to retrieve artifacts from in here.
    // You should only use this when depending on other mods because
    // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html
    // for more information about repositories.
    maven {
        url = "https://api.modrinth.com/maven"
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // Jacoco test coverage
    jacocoAgent "org.jacoco:org.jacoco.agent:${jacoco.toolVersion}:runtime"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.14"
}

configurations { jacocoAgent }

def mixinExclude = '**/offlineplayersreworked/mixin/**'

jacocoTestReport {
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: mixinExclude)
        }))
    }
}

fabricApi {
    configureTests {
        createSourceSet = true
        modId = "gametest-${project.name}"
        eula = true
    }
}

def runGameTestExec = file("$buildDir/jacoco/runGameTest.exec")

tasks.matching { it.name == 'runGameTest' && it instanceof JavaExec }.configureEach {
    doFirst {
        def agentFile = configurations.jacocoAgent.singleFile
        if (!agentFile.exists()) {
            throw new GradleException("JaCoCo agent not found in configuration 'jacocoAgent'")
        }
        // append the agent arg; append=false overwrites previous runs
        jvmArgs += "-javaagent:${agentFile}=destfile=${runGameTestExec},append=false"
        logger.lifecycle("JaCoCo agent attached to runGameTest -> ${runGameTestExec}")
    }
}

tasks.register('jacocoRunGameTestReport', JacocoReport) {
    group = 'verification'
    description = 'Generates JaCoCo report for runGameTest execution'

    executionData.setFrom(files(runGameTestExec))
    def mainOutput = project.sourceSets.main.output

    classDirectories.setFrom(files(mainOutput.classesDirs.files.collect { dir ->
        fileTree(dir: dir, exclude: mixinExclude)
    }))

    sourceDirectories.setFrom(project.sourceSets.main.allSource.srcDirs)

    reports {
        html.required.set(true)
        xml.required.set(false)
        csv.required.set(false)
        html.outputLocation.set(layout.buildDirectory.dir('jacocoRunGameTestHtml'))
    }
}

tasks.register('runGameTestWithCoverage') {
    group = 'verification'
    description = 'Runs runGameTest with JaCoCo agent and generates coverage report'
    dependsOn 'runGameTest'
    finalizedBy 'jacocoRunGameTestReport'
}


jar {
    from("LICENSE") {
        rename { "${it}_${project.base.archivesName.get()}" }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.archives_base_name

            artifact(remapJar.archiveFile) {
                builtBy remapJar
                classifier null
            }

            artifact(sourcesJar) {
                builtBy remapSourcesJar
                classifier "sources"
            }
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}