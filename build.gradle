plugins {
	id 'fabric-loom' version '1.7-SNAPSHOT'
	id 'maven-publish'
	id 'io.github.juuxel.loom-quiltflower' version '1.7.3'
	id 'io.freefair.lombok' version '8.6'
	id "com.gradleup.shadow" version "8.3.3"
}

version = project.mod_version
group = project.maven_group


configurations {
	extraLibs
}

loom {
	accessWidenerPath = file("src/main/resources/offlineplayersreworked.accesswidener")
	runtimeOnlyLog4j = true
}

base {
	archivesName = project.archives_base_name
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.
	maven {
		url = "https://api.modrinth.com/maven"
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Config dependency
	modImplementation "maven.modrinth:midnightlib:${project.midnightlib_version}"

	// Database dependencies
	implementation "io.jsondb:jsondb-core:${project.jsondb_version}"
	implementation "commons-logging:commons-logging:${project.commonslogging_version}"

	extraLibs "commons-logging:commons-logging:${project.commonslogging_version}"
	extraLibs "io.jsondb:jsondb-core:${project.jsondb_version}"

	extraLibs "com.google.guava:guava:${project.guava_version}"

	extraLibs "maven.modrinth:midnightlib:${project.midnightlib_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

shadowJar {
	configurations = [project.configurations.extraLibs]
	relocate 'io.jsondb', 'com.misterfish.jsondb'
	relocate 'org.apache.commons.logging', 'com.misterfish.commons.logging'
	relocate 'com.google.guava', 'com.misterfish.guava'
	relocate 'org.reflections', 'com.misterfish.reflections'
	archiveClassifier.set('')
}

remapJar {
	input.set shadowJar.archiveFile
	dependsOn shadowJar
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId = project.archives_base_name

			artifact(remapJar.archiveFile) {
				builtBy remapJar
				classifier null
			}

			artifact(sourcesJar) {
				builtBy remapSourcesJar
				classifier "sources"
			}
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}	}}
